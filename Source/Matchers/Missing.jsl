// Copyright Â© 2019, SAS Institute Inc., Cary, NC, USA.  All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

Names Default To Here( 0 );

/*
	Class: UtMissingMatcher
		---Prototype---
		class UtMissingMatcher inherits UtMatcher
		---------------
		Checks if a value is missing.
*/
Define Class(
	"UtMissingMatcher",
	Base Class( UtMatcher ),
	matches = Method( {test expr},
		actual = test expr;
		If( ut is numeric missing( Name Expr( actual ) ),
			::ut match info success(),
			mismatch = Char( actual ) || " was not missing";
			::ut match info failure( mismatch );
		);
	);
	describe = Method( {},
		"missing"
	);
);

// Section: Globals

/* 
	Function: ut missing
		---Prototype---
		ut missing()
		---------------
		Use when expected value should be missing (a dot! see <ut is numeric missing>).
	
	Factory function for <UtMissingMatcher>.

	Example:
		> ut assert that( Expr( 5 + . ), ut missing() );
*/
ut matcher factory( 
	"ut missing",
	Expr(Function( {},
		New Object( UtMissingMatcher() );
	)),
	"ut missing()",
	"Use when expected value should be missing (a dot! see ut is numeric missing).",
	{{"Simple", ut assert that( Expr( 5 + . ), ut missing() ); }}
);