// Copyright Â© 2019, SAS Institute Inc., Cary, NC, USA.  All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

Names Default To Here( 0 );

/*
	Class: UtAsCharMatcher
		---Prototype---
		class UtAsCharMatcher inherits UtMatcher
		---------------
		
		Operates only on string values (not on matchers). 
*/
Define Class(
	"UtAsCharMatcher",
	Base Class( UtMatcher ),
	inner = Empty();
	_init_ = Method( {inner},
		this:inner = inner;
	);
	matches = Method( {test expr},		
		this:inner:matches( Char( test expr ) );
	);
	describe = Method( {},
		"as char " || inner:describe();
	);
);

// Section: Globals

/* 
	Function: ut as char
		---Prototype---
		ut as char( string value )
		---------------
		Compares the given string to the actual value converted to character.

		Factory function for <UtAsCharMatcher>.

	Arguments:
		value - should be a string

	Examples:
		> ut assert that( Expr( [1] || [1] ), ut as char( "[1 1]" ) )
*/
ut matcher factory( "ut as char" );
ut as char = Function( {matcher},
	New Object( UtAsCharMatcher( ut ensure matcher( matcher ) ) );
);
