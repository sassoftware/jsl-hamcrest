// Copyright Â© 2019, SAS Institute Inc., Cary, NC, USA.  All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

Names Default To Here( 0 );

/*
	Class: UtLogMatcher
		---Prototype---
		class UtLogMatcher inherits UtMatcher
		---------------
		
		Matches when the log output from test expr satisfies the given matcher.
*/
Define Class(
	"UtLogMatcher",
	Base Class( UtMatcher ),
	inner = Empty();
	_init_ = Method( {inner},
		this:inner = inner
	);
	matches = Method( {test expr},
		this:inner:matches( Log Capture( test expr ) )
	);
	describe = Method( {},
		"log output " || this:inner:describe()
	);
);

// Section: Globals

/*
	Function: ut log output
		---Prototype---
		ut log output( UtMatcher matcher )
		---------------
		Factory function for <UtLogMatcher>.

	Arguments:
		matcher - any object

	Example:
		> ut assert that( Expr( Show( 5 + 5 ) ), ut log output( "\!N5 + 5 = 10;" ) );
		> ut assert that( Expr( Show( 5 + 5 ) ), ut log output( ut ignoring whitespace( "5 + 5 = 10;" ) ) );
*/
ut matcher factory( "ut log output" );
ut log output = Function( {matcher},
	New Object( UtLogMatcher( ut ensure matcher( Name Expr( matcher ) ) ) )
);

/*
	Function: ut log is
		---Prototype---
		ut log is( UtMatcher matcher )
		---------------
		Alias of <ut log output>. Kept only for backwards compatibility.
*/
ut matcher factory( "ut log is" );
ut log is = Name Expr( ut log output );