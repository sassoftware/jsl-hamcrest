// Copyright Â© 2019, SAS Institute Inc., Cary, NC, USA.  All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

Names Default To Here(0);
// Section: Globals


// Variable: ut test runner preferences path
ut test runner preferences path = "$ADDIN_HOME(com.jmp.jslhamcrest)/TestRunner.PFS";

/*	Function: ut load test runner preferences
		Load and return prefereces from disk, or return the default preferences
*/
ut load test runner preferences = Function( {},
	Try( 
		Parse( Load Text File( ut test runner preferences path ), SuppressErrors ),
		// Default Global Preferences
		Expr(
			Hamcrest Test Runner Preferences(
				GUI Location( "Bottom" ),
				Use Markup( 0 ),
				Show Embedded Log( 1 )
			)
		)
	);
);

/*	Function: ut save test runner preferences
		Save preferences to <test runner preferences path>
*/
ut save test runner preferences = Function( {},
	Save Text File(
		ut test runner preferences path,
		Script Box( Char( Name Expr( ut test runner preferences ) ) ) << Reformat << Get Text
	);
);

/*	Function: ut test runner preference
*/
ut test runner preference = Function( {pref},
	{pPref, nPrefs, iPref},
	
	pPref = As Name( pref );
	nPrefs = N Arg( ut test runner preferences );
	For( iPref = 1, iPref <= nPrefs, iPref++,
		pref = Arg( ut test runner preferences, iPref );
		If( Head( pref ) == Name Expr( pPref ),
			If( N Arg( pref ),
				Return( Arg( pref, 1 ) ),
				Return( Empty() )
			);
		);
	);
	Empty();
);

/*	Function: ut set test runner preference
*/
ut set test runner preference = Function({pref, value},
	{pPref, newPrefs, nPrefs, iPref, newPref},
	
	pPref = As Name( pref );
	newPrefs = Head( ut test runner preferences );
	nPrefs = N Arg( ut test runner preferences );
	
	For( iPref = 1, iPref <= nPrefs, iPref++,
		pref = Arg( ut test runner preferences, iPref );
		If( Head( pref ) == Name Expr( pPref ),
			newPref = Head( pref );
			Insert Into( newPref, value );
			Insert Into( newPrefs, Name Expr( newPref ) );
		,
			Insert Into( newPrefs, Name Expr( pref ) );
		);
		
	);	
	ut test runner preferences = Name Expr( newPrefs );
	ut save test runner preferences();
	1;
);

// Variable: ut test runner preferences
ut test runner preferences = ut load test runner preferences();

// Function: ut open test runner preferences gui
ut open test runner preferences gui = Function({},
	{positions, selected position},
	positions = {"Bottom", "Side"};
	selected position = Contains( positions, ut test runner preference( "GUI Location" ) );
	New Window("Hamcrest Test Runner Preferences",
		<<Modal,
		Lineup Box(N Col(2), 
			Text Box("GUI Location", <<Vertical Alignment( "Center" ) ),
			window:position cmb = Combo Box( positions, <<Set( selected position ) ),
			Text Box( "Use Markup for Results" ),
			window:markup chk = Check Box( "", <<Set( 1, ut test runner preference( "Use Markup" ) ) ),
			Text Box( "Show Embedded Log" ),
			window:log chk = Check Box( "", <<Set( 1, ut test runner preference( "Show Embedded Log" ) ) )
		),
		Button Box( "OK",
			ut set test runner preference( "GUI Location", window:position cmb << Get Selected() );
			ut set test runner preference( "Use Markup", window:markup chk << Get( 1 ) );
			ut set test runner preference( "Show Embedded Log", window:log chk << Get( 1 ) );
		)
	)
);
