// Copyright © 2019, SAS Institute Inc., Cary, NC, USA.  All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

Names Default To Here( 0 );

/* 
	Class: UtTestCaseFixture
		---Prototype---
		class UtTestCaseFixture
		---------------
		Holds information about a Test Case.
*/
Define Class( "UtTestCaseFixture",
	// Variable: self
	// Self reference.
	self = Empty();
	// Variable: name
	// Name of the test case
	name = "";
	// Variable: setup expr
	setup expr = Empty();
	// Variable: teardown expr 
	teardown expr = Empty();
	// Variable: skip flag
	skip expr = Expr(0);
	// Variable: skip description
	skip description = "";

	/*
		Method: _init_
			Constructor for ut test case
			
		Arguments:
			name - name of the test case
	*/
	_init_ = Method( {name}, 
		this:name = name;
	);

	Get Test Case Name = Method({}, this:name );

	/* 
		Method: Setup
			Sets a setup expression to be run before each test is 
			evaluated.
		
		Arguments: 
			setup expr - expression to run before tests
			
		Returns:
			Reference to this object
	*/
	Setup = Method( {setup expr},
		If( !Is Expr( Name Expr( setup expr ) ), Throw( "ERROR: Setup must be an expression." ) );
		this:setup expr = Name Expr( setup expr );
		this:self;
	);

	// Method: Get Setup
	Get Setup = Method({}, Name Expr( this:setup expr ) );

	/* 
		Method: Teardown
			Sets a teardown expression to be run after each test is 
			evaluated.
		
		Arguments: 
			teardown expr - expression to run after tests
			
		Returns:
			Reference to this object
	*/
	Teardown = Method( {teardown expr},
		If( !Is Expr( Name Expr( teardown expr ) ), Throw( "ERROR: Teardown must be an expression." ) );
		this:teardown expr = Name Expr( teardown expr ); 
		this:self;
	);

	// Method: Get Teardown
	Get Teardown = Method({}, Name Expr( this:teardown expr ) );
	
	/* 
		Method: Skip If
			Determines if all tests under this test case should be skipped. The skip expr
			should eval to a 0 or 1. It is evaled before each test is started. When the skip expr
			returns 1, the skip description will be used when reporting the skip for each test.
			If the skip description is not provided, the character representation of the expr will
			be used instead.
			
			If you need this to skip only some of the tests under a test case, I would recommend making
			a separate test case for these rather than try to make the skip expr more dynamic.
		
		Arguments: 
			skip expr - determine if all cases should be skipped (eval delayed until ut test)
			skip description - human-readable description of the reason for skip (used if skipped)
			
		Returns:
			Reference to this object
	*/
	Skip If = Method({skip expr, skip description=Empty()},
		this:skip expr = Name Expr( skip expr );
		this:skip description = If(Is Empty(skip description), Char(Name Expr(skip expr)), skip description);
		this:self;
	);
	
	// Method: Get Skip If
	Get Skip If = Method({}, Name Expr( this:skip expr ) );
	
	// Method: Get Skip Description
	Get Skip Description = Method({}, this:skip description );
);

/* 
	Class: UtLogBenchmark
		---Prototype---
		class UtLogBenchmark
		---------------
		Holds log benchmarking information for use in <ut test>.
*/
Define Class(
	"UtLogBenchmark",
	do log bench = 1;
	matcher = "";
	_init_ = Method({do log bench,matcher},
		this:do log bench = do log bench;
		this:matcher = matcher;
	);
	Get Matcher = Method( {}, this:matcher );
	Get Do Log Bench = Method( {}, this:do log bench );
);

// Section: Globals

/*
	Function: ut test case
		Used to isolate a test case from other code in the test file.
		Also prepends the name of the test and assert number to the beginning
		of the labels for ut assert that.
		
	Arguments:
		name - Name of the test case to be used as part of the label.
	
	Returns:
		<UtTestCaseFixture> object which can be used to run tests.
		
	Example:
		Use <UtTestCaseFixture> object to avoid repeating labels.
		------------JSL------------
		test case = ut test case( "Sample test case" );
		ut test( test case, "test level comment - addition",
			Expr(
				ut assert that( Expr( 5 + 6 ), 11 );
				ut assert that( Expr( 5 + 5 ), 10, "my assert level comment" ); 
			)
		);
		ut test( test case, "test level comment - subtraction",
			Expr(
				ut assert that( Expr( 5 - 6 ), -1 );
				ut assert that( Expr( 5 - 5 ), 0, "my assert level comment" ); 
			)
		);
		---------------------------
		
		Create a <UtTestCaseFixture> object to also re-use setup and teardown scripts.
		------------JSL------------
		test case = ut test case( "Sample test case" )
			<< Setup( Expr( dt = Open( "$SAMPLE_DATA\Big Class.jmp" ); ) )
			<< Teardown( Expr( Close( dt, NoSave ); ) );
			
		ut test( test case, "test level comment",
			Expr(
				dt << new column( "Test", Formula( Row() ) );
				ut assert that( Expr( dt:Test << Get Values ), (1::40)`, "my assert level comment" ); 
			)
		);
		ut test( test case, "test level comment",
			Expr(
				dt << new column( "Test", Value Labels( {0 = "N", 1 = "Y"} ) );
				ut assert that( Expr( dt:Test << Get Property( "Value Labels" ) ), Expr( Value Labels( {0 = "N", 1 = "Y"} ) ), "my assert level comment" );
			)
		);
		---------------------------
*/
ut define documented function(
	"ut test case",
	Expr(Function({name},
		ut new object( "UtTestCaseFixture", Eval List({name}) );
	)),
	"ut test case( test case name )",
	"Used to isolate a test case from other code in the test file. Also prepends the name of the test and assert number to the beginning of the labels for ut assert that.",
	{
		{
			"Setup/Teardown",
			test case = ut test case( "Sample test case" )
				<< Setup( Expr( dt = Open( "$SAMPLE_DATA\Big Class.jmp" ); ) )
				<< Teardown( Expr( Close( dt, NoSave ); ) );
				
			ut test( test case, "test level comment",
				Expr(
					dt << new column( "Test", Formula( Row() ) );
					ut assert that( Expr( dt:Test << Get Values ), (1::40)`, "my assert level comment" ); 
				)
			);
			ut test( test case, "test level comment",
				Expr(
					dt << new column( "Test", Value Labels( {0 = "N", 1 = "Y"} ) );
					ut assert that( Expr( dt:Test << Get Property( "Value Labels" ) ), Expr( Value Labels( {0 = "N", 1 = "Y"} ) ), "my assert level comment" );
				)
			);
		},
		{
			"Repeating Labels",
			test case = ut test case( "Sample test case" );
			ut test( test case, "test level comment - addition",
				Expr(
					ut assert that( Expr( 5 + 6 ), 11 );
					ut assert that( Expr( 5 + 5 ), 10, "my assert level comment" ); 
				)
			);
			ut test( test case, "test level comment - subtraction",
				Expr(
					ut assert that( Expr( 5 - 6 ), -1 );
					ut assert that( Expr( 5 - 5 ), 0, "my assert level comment" ); 
				)
			);
		},
		{
			"Skip If",
			test case = ut test case( "Sample test case" )
				<< Skip If( Expr( Num(Regex(JMP Version(), "(\d+)", "\1")) > 15 ), "JMP Version > 15" );
				
			ut test( test case, "test level comment",
				Expr(
					ut assert that( Expr( 1 + 1 ), 2 );
				)
			);
		}
	}
);

/* 
	Class: UtSuccessFilteringReporter
		---Prototype---
		class UtSuccessFilteringReporter inherits UtReporter
		---------------
		
		Forwards only non-successful events to an inner reporter.
*/
Define Class("UtSuccessFilteringReporter",
	Base Class( "UtReporter" ),
	inner = Empty();
	_init_ = Method( {inner},
		this:inner = inner
	);
	add expression failure = Method( {label, payload=Empty()},
		this:inner << add expression failure( label )
	);
	add failure = Method( {label, test expr, description, mismatch, lre, payload=Empty()},
		this:inner << add failure( label, Name Expr( test expr ), description, mismatch, lre, Name Expr( payload ) )
	);
	add unexpected throw = Method( {label, test expr, description, exception, payload=Empty()},
		this:inner << add unexpected throw( label, Name Expr( test expr ), description, exception, Name Expr( payload ) )
	);
	add success = Method( {label, test expr, description, payload=Empty()},
		.
	);
	add skip = Method( {label, test expr, description, skip description, payload=Empty()},
		// Counted as a success for now.
		// This is used for the log benchmarking which has an independent skipping mechanism. 
		.
	);
	release = Method({},
		First(this:inner, this:inner = Empty())
	);
);

/* 
	Class: UtBufferingReporter
		---Prototype---
		class UtBufferingReporter inherits UtReporter
		---------------
		
		Holds buffer of events until release is called.
*/
Define Class(
	"UtBufferingReporter",
	Base Class( "UtReporter" ),
	buffer = {};
	inner = Empty();
	_init_ = Method( {inner},
		this:inner = inner
	);
	add expression failure = Method( {label, payload = Empty()},
		Insert Into( buffer, 
			Eval Expr( 
				this:inner << add expression failure( 
					Expr( label ), 
					Expr( ut expr literal( Name Expr( payload ) ) )
				) 	
			) 
		)
	);
	add failure = Method( {label, test expr, description, mismatch, lre, payload = Empty()},
		Insert Into( buffer,
			Eval Expr(
				this:inner << add failure(
					Expr( label ),
					Expr( ut expr literal( Name Expr( test expr ) ) ),
					Expr( description ),
					Expr( mismatch ),
					Expr( lre ),
					Expr( ut expr literal( Name Expr( payload ) ) )
				)
			)
		)
	);
	add unexpected throw = Method( {label, test expr, description, exception, payload = Empty()},
		Insert Into( buffer,
			Eval Expr(
				this:inner << add unexpected throw(
					Expr( label ),
					Expr( ut expr literal( Name Expr( test expr ) ) ),
					Expr( description ),
					Expr( exception ),
					Expr( ut expr literal( Name Expr( payload ) ) )
				)
			)
		)
	);
	add success = Method( {label, test expr, description, payload = Empty()},
		Insert Into( buffer,
			Eval Expr(
				this:inner << add success(
					Expr( label ),
					Expr( ut expr literal( Name Expr( test expr ) ) ),
					Expr( description ),
					Expr( ut expr literal( Name Expr( payload ) ) )
				)
			)
		)
	);
	add skip = Method( {label, test expr, description, skip description, payload=Empty()},
		Insert Into( buffer,
			Eval Expr(
				this:inner << add skip(
					Expr( label ),
					Expr( ut expr literal( Name Expr( test expr ) ) ),
					Expr( description ),
					Expr( skip description ),
					Expr( ut expr literal( Name Expr( payload ) ) )
				)
			)
		)
	);
	release = Method( {},
		Eval List( buffer );
		buffer = {};
		First( this:inner, this:inner = Empty() );
	);
);

/* 
	Function: ut test log benchmark label
		-------------Prototype-------------
		ut test log benchmark label(
			string	case_name
			string	test_name
		);
		-----------------------------
		
		Returns	the label used when a test log benchmark fails. This is
		an extension point for ut test and can be overwritten to customize
		the label.
*/
ut test log benchmark label = Function({case name, test name},
	Eval Insert( "^ut concat test label( case name, test name, \!"Log Benchmark\!" )^" )
);

/* 
	Function: ut test
		-------------Prototype-------------
		ut test(
			UtTestCaseFixture	test_case,
			string 				test_name,
			expression 			test_expr,
			UtLogBenchmark	 	log_benchmark 		= ut log bench( 1, "" )
		);
		-----------------------------
		
		Run a test expression. Runs any given setup and tear down expressions.
		Reports any unexpected exceptions as failures.
		
		The log benchmark argument determines if the log is captured and what
		is asserted about the captured log (if anything). The default is
		managed by the <ut log bench> and <ut log bench default>.
		
	Arguments: 
		test_case - name of test case or class object reference to test case. See <ut test case>.
		test_name - name of the test.
		test_expr - expression to run test.
		log_benchmark - if and how to benchmark the log
		
	Returns:
		1 if all tests succeeded, otherwise 0.
		
	Example:
		------------JSL------------
		ut test( "TC #5", "addition",
			Expr(
				ut assert that( Expr( 5 + 6 ), 11 );
				ut assert that( Expr( 5 + 5 ), 10, "my assert level comment" ); 
			),
			ut log bench( 1, "" ) // Capture the log and assert that it is empty
		);
		---------------------------
*/
ut define documented function(
	"ut test",
	Expr(Function({test case, test name, test expr, log benchmark = ut log bench()},
		
		// Validate inputs
		If( 
			Is String( Name Expr( test case ) ),
				test case = ::ut test case( test case );
		,
			!Is Class( Name Expr( test case ) ),
				Throw( "ut test() requires the first argument to be a string or test case object." );
		,
			test case << Get Name != "UtTestCaseFixture",
				Throw( "ut test() requires the first argument to be a string or test case object." );
		);
		
		If( !Is String( Name Expr( test name ) ),
			Throw( "ut test() requires the second argument to be a string." );
		);
		
		If( !Is Expr( Name Expr( test expr ) ),
			Throw( "ut test() requires the third argument to be an expression." );
		);
		
		If( !Is Class( Name Expr( log benchmark ) ),
				Throw( "ut test() requires the optional fourth argument to be ut log bench( < 0 | 1 >, < string matcher > )" );
		,
			log benchmark << Get Name != "UtLogBenchmark",
				Throw( "ut test() requires the optional fourth argument to be ut log bench( < 0 | 1 >, < string matcher > )" );
		);
		
		// Create an anonymous namespace that is not in the current
		// dynamic scoping hierarchy.
		// This variable is the only thing that could potentially collide 
		// with a test.
		local:_utTestNS = ut move to anonymous namespace( Namespace( "local" ) );
		
		// get the symbols defined before running the test case
		_utTestNS:before symbols = ::ut get symbol information();
		// get the current row before running the test case
		_utTestNS:before row = Row();
		
		_utTestNS:n asserts = 0;
		_utTestNS:results = {};
		
		For( _utTestNS:iAssertion = 1, _utTestNS:iAssertion <= N Items( ut test registered assertions ), _utTestNS:iAssertion++,
			Eval( 
				ut test assertion decorator( 
					ut test registered assertions[_utTestNS:iAssertion][1], 
					ut test registered assertions[_utTestNS:iAssertion][2]
				)
			);
		);
		
		_utTestNS:run setup expr = Expr(
			// evaluate the setup expression
			Try( 
				Eval( _utTestNS:test case << Get Setup() );
				Names Default to Here( 0 );
				1;
			,
				Names Default to Here( 0 );
				ut global reporter << Add Unexpected Throw( 
					ut concat test label(
						_utTestNS:test case << Get Test Case Name(),
						_utTestNS:test name,
						"Setup"
					),
					ut get exception expression( exception_msg ),
					"test case setup does not throw...skipping test...",
					::ut get exception message( exception_msg );
				);
				0;
			);
		);
		
		_utTestNS:run test expr = Expr(
			// evaluate the test expression
			Try( 
				Eval( _utTestNS << Get Value("test expr") );
				Names Default to Here( 0 );
			,
				Names Default to Here( 0 );
				_utTestNS:temp label = ut concat test label(
					_utTestNS:test case << Get Test Case Name(),
					_utTestNS:test name,
					"Test"
				);
				ut global reporter << Add Unexpected Throw( 
					_utTestNS:temp label,
					ut get exception expression( exception_msg ),
					"test does not throw outside of an assertion",
					::ut get exception message( exception_msg );
				);
			);
		);
		
		_utTestNS:run teardown expr = Expr(
			// evaluate the teardown expression
			Try( 
				Eval( _utTestNS:test case << Get Teardown() );
				Names Default to Here( 0 );
			,
				Names Default to Here( 0 );
				ut global reporter << Add Unexpected Throw( 
					ut concat test label(
						_utTestNS:test case << Get Test Case Name(),
						_utTestNS:test name,
						"Teardown"
					),
					ut get exception expression( exception_msg ),
					"test case teardown does not throw",
					::ut get exception message( exception_msg );
				);
			);
		);
		
		// evaluate the skip if expression
		_utTestNS:skip? =
			Try(
				First(
					Eval( _utTestNS:test case << Get Skip If() ),
					Names Default to Here( 0 )
				)
			,
				Names Default to Here( 0 );
				ut global reporter << Add Unexpected Throw( 
					ut concat test label(
						_utTestNS:test case << Get Test Case Name(),
						_utTestNS:test name,
						"Skip If"
					),
					ut get exception expression( exception_msg ),
					"test case skip if does not throw",
					::ut get exception message( exception_msg );
				);
				0 // Don't skip (fairly arbitrary choice)
			);
			
		If(!Is Number(Name Expr(_utTestNS:skip?)) | Is Missing(_utTestNS:skip?),
			ut global reporter << Add Unexpected Throw( 
				ut concat test label(
					_utTestNS:test case << Get Test Case Name(),
					_utTestNS:test name,
					"Skip If"
				),
				Name Expr(_utTestNS:test case << Get Skip If() ),
				"test case skip returns non-missing number",
				Eval Insert("returned ^ut get show string(Name Expr(_utTestNS:skip?))^")
			);
		);
		
		If( _utTestNS:skip?,
			ut global reporter << Add Skip( 
				ut concat test label(
					_utTestNS:test case << Get Test Case Name(),
					_utTestNS:test name,
					"Skip"
				),
				Empty(), /*Empty for entire test skip*/
				"", /*Empty for entire test skip*/
				_utTestNS:test case << Get Skip Description()
			);
		,
			// Get Do Log Bench() < 0 means don't capture
			_utTestNS:log benchmark << Get Do Log Bench() >= 0
		,
			// Replace reporter so event handlers don't happen under Log Capture
			_utTestNS:buffering reporter = New Object("UtBufferingReporter"(ut global reporter));
			ut global reporter = _utTestNS:buffering reporter;
			
			// Do Setup, Body, Teardown under Log Capture
			_utTestNS:log = Log Capture( _utTestNS:setup success = Eval( _utTestNS << Get Value( "run setup expr" ) ) );
			If( _utTestNS:setup success,
				_utTestNS:log ||= Log Capture( Eval( _utTestNS << Get Value( "run test expr" ) ) );
			);
			_utTestNS:log ||= Log Capture( Eval( _utTestNS << Get Value( "run teardown expr" ) ) );
			
			// Release results to original reporter and restore it
			ut global reporter = _utTestNS:buffering reporter << release();
			
			// Do benchmark if requested
			If( _utTestNS:log benchmark << Get Do Log Bench(),
				Local({log contents},
					// Replace reporter so we supress success events
					_utTestNS:filtering reporter = New Object("UtSuccessFilteringReporter"(ut global reporter));
					ut global reporter = _utTestNS:filtering reporter;
					
					log contents = _utTestNS:log;
					// Qualified so that the ut test log benchmark label
					// is used without modification
					_utTestNS:n asserts++;
					Insert Into(_utTestNS:results,
						::ut assert that(
							Expr( log contents ), 
							_utTestNS:log benchmark << Get Matcher(),
							ut test log benchmark label(_utTestNS:test case << Get Test Case Name, _utTestNS:test name)
						);
					);
					
					ut global reporter = _utTestNS:filtering reporter << release();
				)
			)
		,
			// Else
			_utTestNS:setup success = Eval( _utTestNS << Get Value( "run setup expr" ) );
			If( _utTestNS:setup success,
				Eval( _utTestNS << Get Value("run test expr") );
			);
			Eval( _utTestNS << Get Value( "run teardown expr" ) );
		);
		
		// get the symbols after the test case is run
		_utTestNS:after symbols = ::ut get symbol information();
		
		// delete the symbols that were added during the test
		::ut delete symbols difference( _utTestNS:before symbols, _utTestNS:after symbols, 1 /* Close Windows */ );
		// restore the current row in case it was changed
		Row() = _utTestNS:before row;
		
		// Return whether or not all asserts passed
		First( All( _utTestNS:results ), Delete Namespaces( Force( 1 ), _utTestNS ) );
	)),
	"ut test( test case | test case name, test name, body, <log bench> )",
	"Run a test expression. Runs any given setup and tear down expressions. Reports any unexpected exceptions as failures.\!n\!n" || 
	"The log benchmark argument determines if the log is captured and what is asserted about the captured log (if anything). The default is managed by the ut log bench and ut log bench default.",
	{{
		"Simple", 
		"\[
ut test( "TC #5", "addition",
	Expr(
		ut assert that( Expr( 5 + 6 ), 11 );
		ut assert that( Expr( 5 + 5 ), 10, "my assert level comment" ); 
	),
	ut log bench( 1, "" ) // Capture the log and assert that it is empty
);
]\"
	}}
);
/*	Function: ut log bench
		-------------Prototype-------------
		ut log bench(
			integer	  do_log_bench = ut_log_bench_default,
			UtMatcher matcher = ""
		);
		-----------------------------

		do log bench args
		-  0: Capture the log and throw it away (no benchmark, matcher not run)
		-  1: Capture the log and assert that contents satisfy the matcher
		- -1: Do not capture the log (no benchmark, matcher not run)

	 		Factory function for <UtLogBenchmark>

	Arguments:
		do_log_bench - See description above.
		matcher - Used for assertion on contents of log (when benchmarked)
*/
ut log bench = Function({do log bench=ut log bench default, matcher=""},
	New Object( UtLogBenchmark( do log bench, ut ensure matcher( Name Expr( matcher ) ) ) );
);

// Variable: ut log bench default
ut log bench default = -1;
// Variable: ut log bench off
ut log bench off = ut log bench(0);
// Variable: ut log bench no capture
ut log bench no capture = ut log bench(-1);
// Variable: ut log bench on
ut log bench on = ut log bench(1);

// Variable: ut test registered assertions
ut test registered assertions = {};

/*	Function: ut test register assertion
		Register a function to be handled within <ut test>.

		Allows <ut test> to decorate registered assertions. All 
		arguments will be passed verbatim except for a label 
		argument, which will be transformed by <ut test> using
		<ut concat test label> with the test case name, test 
		name, and number of assertions.

		The given args will be the signature of the decorator
		function. This should match the signature of your
		original assertion function. Variable names are not
		important except for label. However, defaults are 
		important and should match. 

		Your assertion function must return a boolean 1 or 0
		and should send results to <ut global reporter>.

		The end result of a registered assertion function is
		the ability to run your assertion inside of a test.
		It will have an augmented label and the assertion 
		result will affect the result of the entire test.

	Example:
		---JSL---
		my assertion = Function( {truth, label=""}, 
			do stuff; 
			ut global reporter:add xxx(...); 
			Random Integer( 0, 1 )
		);
		ut test register assertion( "my assertion", {truth, label=""} );
		---------
*/
ut test register assertion = Function( {function name, args},
	Insert Into( ut test registered assertions, Eval List( {Eval List( {function name, args} )} ) );
);

/*	Function: ut test assertion decorator
		Builds a local assignment of an assertion
		function for decorating within <ut test>.
*/
ut test assertion decorator = Function( {function name, args},
	{inner call, i, arg},
	
	inner call = Parse( "::" || function name );
	
	For( i = 1, i <= N Items( args ), i++,
		arg = If( Head( args[i] ) == Expr( Assign ), Arg( args[i], 1 ), args[i] );
		
		If( Name Expr( arg ) == Expr( label ),
			Insert Into( inner call,
				Expr(
					ut concat test label(
						_utTestNS:test case << Get Test Case Name(), 
						_utTestNS:test name,
						Name Expr( label ),
						_utTestNS:n asserts
					)
				)
			),
			Insert Into( inner call, Eval Expr( Name Expr( Expr( Name Expr( arg ) ) ) ) )
		);
	);
	Eval Expr(
		As Scoped( local, Expr( function name ) ) = Function( Expr( args ),
			_utTestNS:n asserts++;
			Insert Into( _utTestNS:results, 
				Expr( Name Expr( inner call ) );
			);
			_utTestNS:results[N Items( _utTestNS:results )];
		)
	)
);

/*	Function: ut expand test
		Accepts same arguments as <ut test>, but returns you an expression with 
		all parts expanded. Might be useful for debugging your script.
		
		Note, this currently does not include the Skip If expression.
	
		-------------Prototype-------------
		ut expand test(
			UtTestCaseFixture	test_case,
			string 				_dummy1,
			expression 			test_expr,
			missing			 	_dummy2 		= .
		);
		-----------------------------
	
	Arguments:
		test_case - test case or string used
		_dummy1 - variable is not used, but is kept as placeholder to match <ut test>
		test_expr - the main test expression
		_dummy2 - variable is not used, but is kept as placeholder to match <ut test>
	
	Example:
		---JSL
		
		test case = ut test case( "Sample test case" )
			<< Setup( Expr( dt = Open( "$SAMPLE_DATA\Big Class.jmp" ); ) )
			<< Teardown( Expr( Close( dt, NoSave ); ) );
			
		ut expand test( test case, "test level comment",
			Expr(
				dt << new column( "Test", Formula( Row() ) );
				ut assert that( Expr( dt:Test << Get Values ), (1::40)`, "my assert level comment" ); 
			)
		);
		---
*/
ut expand test = Function( {test_case, _dummy1, test_expr, _dummy2=.},
	{outputExpr,insert into output expr},
	
	If( 
		Is String( test_case ),
			Name Expr( test_expr );
	,
		//else
			ut merge expressions( Eval LIst( {Name Expr( test_case:setup expr ), Name Expr( test_expr ), Name Expr( test_case:teardown expr ) } ) );
	)
);
