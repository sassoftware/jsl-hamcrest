// Copyright © 2019, SAS Institute Inc., Cary, NC, USA.  All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

Names Default To Here( 0 );

/*	Variable: ut matcher factories
		List of head expressions for all matcher functions.

		Used by <UtExpressionCompositeMatcher>
*/
ut matcher factories = {};

// Variable: ut documented functions
ut documented functions = {};

// Variable: ut should deploy documented functions
// Only overwritten if not already set. Default is 1.
If( Is Empty( ut should deploy documented functions ), 
	ut should deploy documented functions = 1;
);

ut deploy documented functions = Function({},
	If( ut should deploy documented functions,
		Log Capture( Add Custom Functions( ut documented functions ) );
		// Clear memory and make room to call again with new fuctions
		ut documented functions = {};
	);
	ut nullptr;
);

/*	Function: ut matcher factory
		---Prototype---
		ut matcher factory( String name, Function func, String prototype = "", String description = "", List<List> examples )
		---------------
		Function used to insert their names into the ut matcher 
		factories list.
		
	Arguments:
		name - string name for the function
*/
ut matcher factory = Function( {name, function decl=Empty(), prototype="", description="", examples={}}, 

	If( !Contains( ut matcher factories, As Name( name ) ),
		Insert Into( ut matcher factories, As Name( name ) );
	);
	If( !Is Empty( Name Expr( function decl ) ),
		ut define documented function( name, Name Expr( function decl ), prototype, description, examples );
	);
	ut nullptr;
);

/*  Function: ut define custom function
		Define a documented function in the Scripting Index
*/
ut define documented function = Function({name, function decl, prototype, description, examples},
	{cf, i},
	// add a copy of the function to the package namespace and scripting index.
	If( ut should deploy documented functions,
		cf = New Custom Function( "global", name, function decl,
			<< Scripting Index Category( ut package name ),
			<< Prototype( prototype ),
			<< Description( description )
		);
		For( i = 1, i <= N Items( examples ), i++,
			If( N Items( examples[i] ) != 2,
				Throw("Examples must contain a name and script."),
			);
			// A string can be used for an example to preserve
			// whitespace and comments.
			If( Is String( examples[i][2] ),
				cf << Example( "Names Default To Here( 0 );\!nut global reporter = ut streaming log reporter with skip();\!n" || examples[i][2], examples[i][1] );
			,
				cf << Example( ut merge expressions( Eval List( {Expr(Names Default To Here( 0 ); ut global reporter = ut streaming log reporter with skip(); ), examples[i][2]} ) ), examples[i][1] );
			);
		);
		Insert Into( ut documented functions, cf );
	);
	// Always assign the ut global function, for backwards compatibility
	As Scoped( "global", name || "" ) = function decl;
);

/*	Function: ut is a matcher
		This determines (through duck typing) if the given object is
		likely a matcher. 

		Returns:
			1 if maybe matcher is (probably) a matcher, 0 otherwise

		Parameters:
			maybe matcher - any object
*/
ut is a matcher = Function( {maybe matcher},
	Try( 
		If( Type( Name Expr( maybe matcher) ) == "Class",
			Name Expr( maybe matcher ) << Contains( "describe" ),
			0	
		),
		0
	)
);

ut ensure matcher default denylist = {"UtGenuineSkipMatcher"};

/*	Function: ut ensure matcher
		Conditionally wraps its argument so that the output is always
		a matcher. Does nothing if the argument is already a matcher.
		Wraps with the <Ut Equal To Matcher> otherwise. Often used
		in matcher factory functions.
		
		Accepts an optional denylist which disallows certain matchers
		from being accepted as maybe matcher. This is mainly to prevent
		matchers that don't make much sense from being used as inner
		matchers in maatcher factories. Some matchers are denied by
		default. If you want to allow everything, pass in an explicit {}.
		This function throws when a denylist matcher is provided.

		Returns:
			A matcher (pass-through or simply wrapped)

		Parameters:
			maybe matcher - any object
			denylist - a list of class names
			
*/
ut ensure matcher = Function( {maybe matcher, denylist=ut ensure matcher default denylist},
	If( ut is a matcher( Name Expr( maybe matcher ) ),
		If(Contains(denylist, maybe matcher << Get Name),
			Throw(Eval Insert("Invalid inner matcher ^maybe matcher << Get Name^")));
		Name Expr( maybe matcher ),
		ut equal to( Name Expr( maybe matcher ) )
	)
);

/*
	Function: ut ensure matcher list
		---Prototype---
		ut ensure matcher list( List<UtMatcher> maybe_matchers )
		---------------
		Convert a list of expressions to a list of matchers
		based on their head expressions.
	
	Arguments:
		maybe_matchers - list of expressions
*/
ut ensure matcher list = Function({matchers},
	{i},
	For( i = 1, i <= N Items( matchers ), i++,
		matchers[i] = If( Contains( ::ut matcher factories, Head( matchers[i] ) ),
			Eval( matchers[i] ),
			ut ensure matcher( matchers[i] )
		);
	);
	matchers;
);

/*	Function: ut global lre
		Redefine <ut global lre> to customize how LRE is computed. Default
 		behavior is to always return missing (LRE unsupported).

 		<ut global lre> is used by the Equal To and Close To matchers
	 	to report the LRE value based on actual and expected values.
*/
ut global lre = Function( {actual, expected},
	{i = 1, compute lre},
	compute lre = Function( {actual, expected},
		If( (actual - expected) == 0, 
				16,
			expected != 0 & actual != 0,
				-Log10( Abs( (actual - expected) / Abs( expected ) ) ),
			actual != 0, 
				-Log10( Abs( actual ) ),
				-Log10( Abs( expected ) )
		)
	);
	If(
		Is Number( Name Expr( actual ) ) & Is Number( Name Expr( expected ) ),
			compute lre( actual, expected ),
		Is Matrix( Name Expr( actual ) ) & Is Matrix( Name Expr( expected ) ),
			If( N Row( actual ) == N Row( expected ) & N Col( actual ) == N Col( expected ),
				J( N Row( actual ), N Col( actual ), compute lre( actual[i], expected[i++] ) )
			)
	);
);

/*	Function: ut unregister matcher factory
		Removes a matcher factory name from
		<ut matcher factories>. 
		
		This should be used when creating your 
		own matcher outside of the Hamcrest
		package. Unregistering a matcher 
		removes it from consideration in
		<UtExpressionCompositeMatcher>.
*/
ut unregister matcher factory = Function( {name},
	{pos},
	pos = Contains( ut matcher factories, As Name( name ) );
	If( pos,
		Remove From( ut matcher factories, pos );
	);
);


/*	Function: ut with reporter
		Temprorarily sets <ut global reporter>
		to the given reporter and resets it back
		to its original state, even if the 
		expression threw an error.
*/
ut with reporter = Function({reporter, expression},
	{global reporter},
	global reporter = ut global reporter;
	ut global reporter = reporter;
	Try(
		First(
			expression,
			ut global reporter = global reporter
		),
		ut global reporter = global reporter;
		Throw( Char( exception_msg ) );
	);
);

/*
	Function: ut get exception message
	
	Arguments:
		e - List or string value (from exception_msg global variable set within a Try())

	Returns:
		String representing the different parts of the exception_msg 
		variable.
*/
ut get exception message = Function({e},
	{Default Local},
	If( IsString(e), Return( e ));
	If( !IsList(e), Return( "Unknown" ) );
	
	errorList = {};
	
	for(i=1,i<=nitems(e),i++,
		err = e[i];
		
		temp = Head Name( err );
		If( !Ends With( temp, "." ), temp ||= "." );
		Insert Into( errorList, temp );
		offset=0;

		for(j=1,j<=narg( err ),j++,
			a = Arg( err, j );
			match( j+offset,
				1,
					Insert Into( errorList, Char( a ) || " time" || If MZ(a >1,"(s)","" ) ||"." ),
				
				2,
					if( type( a ) == "List",
						Insert Into( errorList,"Data Table Indices: " || char(a) );
					,
						offset++;
						j--;
					),
				
				3,
					Insert Into( errorList, 
						Match( a,
							0, "Note:",
							1, "Warning:",
							2, "Error:",
							3, "Fatal:"
						),
						1
					),
				
				4,
					If( Type( Arg(err,4-offset) ) == "String",
						.; // Do nothing
					,
						offset++;j--;
					),
				
				5,
					If( Char( Head Name( arg(err,5-offset) ) ) == "Bad Argument",
						Insert Into( errorList, "Bad Argument( " || char( arg(a,1)) || " )" ),
						offset++;j--;
					),
				6,
					Insert Into( errorList, char(nameExpr(a)) );
			)
		);
	);
	
	errStr = Concat Items( errorList, " " );
	
	Return(errStr);
);

/* 
	Function: ut get exception expression
		Takes the exception_msg variable as an argument and extracts
		the expression being executed from the list. If the expression 
		cannot be found, it returns Expr( Unknown Expression ).
*/
ut get exception expression = Function( {e},
	{resultExpr, err, offset, i, j, a},
	
	If( 
		Is String( e ),
			Return( Eval Expr( Throw( Expr( e ) ) ) ),
		!Is List( e ),
			Return( Expr( Unknown Expression ) )
	);
	
	resultExpr = Expr( Glue() );
	
	For( i = 1, i <= N Items( e ), i++,
		err = e[i];
		
		offset = 0;

		For( j = 1, j <= N Arg( err ), j++,
			a = Arg( err, j );
			Match( j + offset, 
				// 1 is required
				2,
					If( Type( a ) != "List",
						offset++;
						j--;
					), 
				// 3 is required
				4,
					If( Type( Arg( err, 4 - offset ) ) != "String",
						offset++;
						j--;
					),
				5,
					If( Head( Arg( err, 5 - offset ) ) != Expr( Bad Argument() ),
						offset++;
						j--;
					),
				6, 
					Insert Into( resultExpr, Name Expr( a ) ) 
			);
		);
	);
	
	If( N Arg( resultExpr ) == 0,
		Return( Expr( Unknown Expression ) ),
		Name Expr( resultExpr )
	);
	
);

/*
	Function: ut extract thrown string
		Get just the first string portion of the exception_msg. If it
		is just a string, return that.
*/
ut extract thrown string = Function({msg},
	If( Is String(msg),
			msg,
		// otherwise
			Char( msg[1] )
	)
);

/*
	Function: ut is comparable
		Returns true when two objects are of the same type.
		This function considers Integers and Numbers to be of the same type.
*/
ut is comparable = Function({objA, objB},
	If( Is Number( Name Expr( objA ) ) & Is Number( Name Expr( objB ) ),
			1,
		// expressions and names are in fact comparable.
		// Using Is Expr() like we did for IsNumber() could give false positives.
		Contains( {"Expression","Name"}, Type( Name Expr( objA ) ) ) & Contains( {"Expression","Name"}, Type( Name Expr( objB ) ) ),
			1,
		Type( Name Expr( objA ) ) == Type( Name Expr( objB ) )
	)
);

/*
	Function: ut is numeric missing
		Returns true only if the argument has the value . (dot)
		
		This is subtly different than just Is Missing().
*/
ut is numeric missing = Function( {obj},
	Is Number( Name Expr( obj ) ) & Is Missing( Name Expr( obj ) )
);

/*
	Function: ut equal with missing
		Compares two values and returns true if they are equal. 
		
		Considers both values being missing as equal.
		
		Uses <ut is numeric missing> to determine missing values

	Arguments:
		a - value to compare
		b - value to compare
		
	History:
		31jul2018/juchil - updated to use <ut is numeric missing> to allow for comparing any value.
*/
ut equal with missing = Function( {a, b},
	If(
		ut is numeric missing( Name Expr( a ) ) & ut is numeric missing( Name Expr( b ) ), 
			1,
		ut is numeric missing( Name Expr( a ) ) != ut is numeric missing( Name Expr( b ) ), 
			0,
		Is Empty( Name Expr( a ) ) & Is Empty( Name Expr( b ) ),
			1,
		Is Empty( Name Expr( a ) ) != Is Empty( Name Expr( b ) ),
			0,
		// else
			a == b
	)
);

/*
	Function: ut get show string
		Converts non-string values to character. 
		If the value is a string, double quotes are concatenated 
		to the beginning and end. This is used for showing values
		in expected and actual results.
*/
ut get show string = Function( {obj},
	If( Is String( Name Expr( obj ) ),
		"\!"" || obj || "\!"",
		Char( Name Expr( obj ) )
	);
);

/*
	Function: ut object supports message
		Checks if the given object allows for receiving a given message.
*/
ut object supports message = Function({obj, msg},
	{propStr, rc},
	If( 
		Is Scriptable( Name Expr( obj ) ),
			// Get a string containing all available messages for the object
			propStr = Log Capture( Show Properties( Name Expr( obj ) ) );
			
			// remove all spaces from the message name
			msg = Substitute( msg, " ", "" );
			
			// put optional spaces between each letter
			msg = concat items( words( msg, "" ), "\s*" );
			
			// Find the message in the Show Properties() output
			// example: Get Title [Action]
			rc = Regex(
				propStr,
				Eval Insert( "\s+(^msg^)\s\!\[" ),
				"\1",
				IGNORECASE
			);
			If( ut is numeric missing( rc ), 0, 1 );
	,
		// else not scriptable
			0
	);
);

/*
	Function: ut detailed type
		Get's the type with extra info where appropriate.
*/
ut detailed type = Function({obj},
	{type},
	
	type = Type( Name Expr( obj ) );
	
	Match( type,
		"DisplayBox",
			Eval Insert( "DisplayBox[^obj << Class Name^]" ),
		"DisplaySeg",
			Eval Insert( "DisplaySeg[^obj << Class Name^]" ),
		"Namespace",
			Eval Insert( "Namespace[^obj << Get Name^]" ),
		"Class",
			Eval Insert( "Class[^obj << Get Name^]" ),
		"List",
			Eval Insert( "List[^N Items( obj )^]" ),
		"Matrix",
			Eval Insert( "Matrix[^N Col( obj )^x^N Row( obj )^]" ),
		"Picture",
			Eval Insert( "Picture[^Concat Items( obj << Get Size, \!"x\!" )^]" ),
		"Column",
			Eval Insert( "Column[^obj << Get Name^]" ),
		"Table",
			Eval Insert( "Table[^obj << Get Name^]" ),
		"Scriptable",
			"Scriptable[" || Substitute( Char( Name Expr( obj ) ), "[]", "", "()", "" ) || "]",
		// default
			type;
	);
);

/*	
	Function: ut host table name str
		Give it the windows table and get back a host specific data table name. (i.e. it appends ".jmp" on Mac to a given string)
		
		For a matcher that uses this function, see <ut host table name>
*/
ut host table name str = Function({tbl_name},
	If( Host Is( "Mac" ),
		tbl_name || ".jmp",
		tbl_name
	)
);

/*	Function: ut as expr
		Converts any value into an expression by wrapping it in Glue().
		
		Great for when you want to use a literal value as the first arg in <ut assert that>
*/
ut as expr = Function( {any},
	Eval Expr( Glue( Expr( any ) ) )
);

/*	Function: ut expr literal
		Wraps the given literal value in an expression.
*/
ut expr literal = Function( {an expr},
	Insert( Expr( Expr() ), Name Expr( an expr ) )
);

/*	Function: ut new object
		---Prototype---
		ut new object( String class_name, List args = {} )
		---------------
		Creates a new object with a self reference.
	
	Arguments:
		class_name - name of the JSL defined class to create
		args - arguments passed to the constructor of the class
	
	Example:
		---JSL---
		Define Class("Foo", 
			self = Empty();
			_init_ = Method({x}, this:x = x);
		);
		obj = ut new object( "Foo", {5} );
		---------
*/
ut new object = Function({class name, args={}}, {obj},
	obj = New Object( class name, args );
	obj:self = obj;
);

// Variable: ut concat test label sep
ut concat test label sep = "⮚";

/*	Function: ut format test label
	 		Concatenates values together for <ut test>
*/
ut format test label = Function({label},
	{labelList},
	labelList = {};
	If( label[1] != "", Insert Into( labelList, label[1] ) );
	If( label[2] != "", Insert Into( labelList, label[2] ) );
	If( label[3] != "", Insert Into( labelList, label[3] ) );
	If( !Is Missing( label[4] ), Insert Into( labelList, Char( label[4] ) ) );

	Concat Items( local:labelList, " " || ut concat test label sep || " " );
);

/*	Function: ut form test label
	 		Creates label list for `<UtReporter>`
*/
ut form test label = Function({test case name, test name, label="", n asserts=.},
        Eval List({test case name, test name, label, n asserts})
);

/*	Function: ut fwd test label
	 		Creates {"", "", label, .} or forwards label list.
	 		To be used in registered assertions when sending labels
	 		to the reporter.
*/
ut fwd test label = Function({label},
	If(Is List(Name Expr(label)),
		label,
		Eval List({"", "", Name Expr(label), .})
	);
);

/*	Function: ut merge expressions
		Merges all expressions in a given list
		with a single glue expression.
*/
ut merge expressions = Function({exprs},
	{baseExpr, anExpr, iExpr, iArg},
	baseExpr = Expr( Glue() );
	For( iExpr = 1, iExpr <= Nitems( exprs ), iExpr++,
		anExpr = exprs[iExpr];
		If( Is Expr( Name Expr( anExpr ) ),
			If( Head( anExpr ) == Expr( Glue() ),
				For( iArg = 1, iArg <= narg( anExpr ), iArg++,
					Insert Into( baseExpr, Arg( anExpr, iArg ) )
				);
			,
				Insert Into( baseExpr, Name Expr( anExpr ) );
			);
		);
	);
	Name Expr( baseExpr );
);

/*	Function: ut move to anonymous namespace
		Takes a namespace and moves all args with values 
		to a new anonymous namespace and 
		removes them from the given namespace.
*/
ut move to anonymous namespace = Function({ns},
	{ans, contents, i},
	ans = New Namespace();
	contents = ns << Get Contents();
	// move contents to new namespace
	For( i = 1, i <= NItems( contents ), i++,
		If( N Items( contents[i] ) > 1,
			ans << Insert( contents[i][1], contents[i][2] )
		)
	);
	// remove symbols from existing ns
	ns << Remove( ns << Get Keys() );
	// return ref to anonymous ns
	ans;
);

// Function:  ut difference between lists
// Returns items in listB NOT in listA.
//
// Parameters:
//	   listA - Potentially smaller list than listB. Probably a subset of listB
//     listB - List that might have items not in listA.
//
// Returns:
//     Items in listB NOT in listA.
ut difference between lists = Function( {listA,listB},
	{default local},
	for( i=nitems( listB ), i >= 1, i--,
		If( Contains( listA, listB[i] ),
			Remove From( listB, i );
		)
	);
	listB;
);

/* 
	Function: ut get symbol information

	Returns:
		An associative array with information 
	
	Author:
		juchil
		
	History:
		17may18 - (juchil) Add windows and data tables.
*/
ut get symbol information = Function({},
	{default local},
	aa = [=>];
	If( ut major jmp version >= 15,
		aa["root"] = Namespace("root") << get keys();
	);
	aa["global"] = Namespace("global") << get keys();
	aa["here"] = Namespace("here") << get keys();
	aa["namespaces"] = Get Namespace Names();
	aa["classes"] = Get Class Names();
	aa["windows"] = Get Window List();
	aa;
);

/* 
	Function: ut delete symbols difference
		Deletes symbols in after list not in before list

	Parameters:
		before - associative array of symbols/namespaces/classes defined before a test file was run.
		after -  associative array of symbols/namespaces/classes defined after a test file was run.

	Author:
		juchil
		
	History:
		17may18 - (juchil) Add windows and data tables.
*/
ut delete symbols difference = Function({before, after, closeWindows=0},
	{ns diff, cls diff, i},
	If( ut major jmp version >= 15,
		Namespace( "root" ) << Remove(
			::ut difference between lists( before["root"], after["root"] )
		);
	);
	
	Namespace( "global" ) << Remove(
		::ut difference between lists( before["global"], after["global"] )
	);
	
	Namespace( "here" ) << Remove(
		::ut difference between lists( before["here"], after["here"] )
	);
	
	ns diff = ::ut difference between lists( before["namespaces"], after["namespaces"] );
	For( i = 1, i <= N Items( ns diff ), i++,
		Delete Namespaces( Force( 1 ), ns diff[i] );
	);

	cls diff = ::ut difference between lists( before["classes"], after["classes"] );
	For( i = 1, i <= N Items( cls diff ), i++,
		Delete Classes( Force( 1 ), cls diff[i] );
	);
	
	If( closeWindows, 
		::ut difference between lists( before["windows"], after["windows"] ) << Close Window();
		Caption( Remove( 1 ) ); // Remove the caption if there is one.
	);
	
	// return null
	::ut nullptr;
);

// Variable: ut lang
// Expression to get the code for the current language (en, de, es, fr, it, ja, ko, zh-CN)
ut lang = Expr(System Service(Get Language Code));

/*	Function: ut localize
	---Prototype---
	ut localize( AssociativeArray aa, String lang ) 
	---------------
		Returns a string from the input associative array
		based on the current language code
	
	Arguments:
		aa - associative array with mappings from language code to value.
		lang - language code to get. defaults to current language code.
	
	Example:
	---JSL---
	ut localize(["en" => "Hello", "es" => "Hola"]);
	---------
*/
ut localize = Function({aa, lang = ut lang},
	If(!(aa << Contains(lang)), 
		If(aa << Contains("en"), 
			lang = "en",
			Throw(EvalInsert("\[Language "^lang^" ^If(lang != "en", "and \!"en\!" ", "")^not found in ^aa^]\"))
		)
	);
	
	aa[lang];
);

/*	Function: ut include jsl files recursively
		Looks for files in a folder recursively
		and uses the Include() function to
		run them.
*/
ut include jsl files recursively = Function( {folder},
	{files, iFile, include exprs={}},
	files = Files In Directory( folder, "recursive" );
	For( iFile = 1, iFile <= N Items( files ), iFile++,
		If( Ends With( Lowercase( files[iFile] ), "jsl" ),
			Insert Into( include exprs, Eval Expr( Include( Expr( folder || "/" || files[iFile] ) ) ) );
		);
	);
	// Eval after loop to avoid name collisions 
	// with files and iFile.
	Eval List( include exprs );
);

// Variable: ut nullptr
// Can be used to supress printing anything to the log when returning from a function
ut nullptr = .;
Clear Globals( ut nullptr ); // Sets to nullptr

// Variable: ut major jmp version
ut major jmp version = Num( Word( 1, JMP Version(), "." ) );
